buildscript {
  dependencies {
    classpath("com.google.cloud.tools:jib-spring-boot-extension-gradle:0.1.0")
    classpath('com.google.cloud.tools:jib-ownership-extension-gradle:0.1.0')
  }
}

plugins {
  id 'org.jetbrains.kotlin.jvm' version '1.5.31'
  id 'org.jetbrains.kotlin.plugin.jpa' version '1.5.31'
  id 'org.jetbrains.kotlin.plugin.spring' version '1.5.31'
  id "org.jetbrains.kotlin.kapt" version "1.5.31"
  id 'org.springframework.boot' version '2.5.5'
  id 'com.diffplug.spotless' version '5.14.2'
  id 'jacoco'
  id "com.gorylenko.gradle-git-properties" version "2.3.1"
  id 'net.researchgate.release' version '2.8.1'
  id 'org.liquibase.gradle' version '2.0.4'
  id 'com.google.cloud.tools.jib' version '3.1.4'
  id 'com.apollographql.apollo3' version '3.0.0-alpha07'
}

apply plugin: 'io.spring.dependency-management'

apply from: 'https://raw.githubusercontent.com/erikhofer/autodiff/1.0.2/autodiff.gradle'
project.autodiff.basePackage = "org.codefreak.codefreak.entity"
project.autodiff.changeLogMaster = "src/main/resources/db/changelog-master.yaml"
project.autodiff.changeLogDiff = project.hasProperty('changeLogDiff')
  ? project.property('changeLogDiff') : "src/main/resources/db/changelog-diff.yaml"

group = 'org.codefreak'
sourceCompatibility = '1.8'

repositories {
  mavenCentral()
  jcenter()
  maven { url 'https://jitpack.io' }
}

release {
  git {
    requireBranch = 'master|v\\d+'
  }
}
// This following is not working properly ATM but is required to keep the gradle and package.json version in sync
//project.tasks.preTagCommit.dependsOn ":client:updateVersionFromGradle"
//project.tasks.commitNewVersion.dependsOn ":client:updateVersionFromGradle"

configurations {
  testImplementation {
    // make sure we are not using JUnit 4 accidentally
    exclude group: 'junit', module: 'junit'
  }
}

dependencies {
  implementation 'org.springframework.boot:spring-boot-starter-aop'
  implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
  implementation 'org.springframework.boot:spring-boot-starter-data-ldap'
  implementation 'org.springframework.boot:spring-boot-starter-mail'
  implementation 'org.springframework.boot:spring-boot-starter-security'
  implementation 'org.springframework.boot:spring-boot-starter-web'
  implementation 'org.springframework.boot:spring-boot-starter-batch'
  implementation 'org.springframework.security:spring-security-ldap'
  implementation 'com.fasterxml.jackson.module:jackson-module-kotlin'
  implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.12.5'
  implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml:2.12.5'
  implementation 'org.jetbrains.kotlin:kotlin-reflect:1.5.31'
  implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.5.31'
  implementation 'com.spotify:docker-client:8.16.0'

  // For creating workspaces
  implementation 'io.fabric8:kubernetes-client:5.5.0'
  implementation 'com.github.fkorotkov:k8s-kotlin-dsl:3.0.1'

  // For workspace communication
  implementation 'com.apollographql.apollo3:apollo-runtime:3.0.0-alpha07'

  implementation 'org.apache.commons:commons-compress:1.21'
  implementation 'org.apache.commons:commons-csv:1.9.0'
  implementation 'commons-io:commons-io:2.11.0'
  implementation 'io.sentry:sentry-logback:4.3.0'
  implementation 'com.github.hsingh:java-shortuuid:2a4d72f'
  implementation 'com.github.Open-MBEE:junit-xml-parser:1.0.0'
  implementation 'org.mitre:openid-connect-client:1.3.3'
  implementation 'org.eclipse.jgit:org.eclipse.jgit:5.13.0.202109080827-r'
  implementation 'org.eclipse.jgit:org.eclipse.jgit.archive:5.13.0.202109080827-r'
  implementation 'org.eclipse.jgit:org.eclipse.jgit.ssh.jsch:5.13.0.202109080827-r'
  implementation 'org.liquibase:liquibase-core:4.4.3'
  implementation 'com.graphql-java-kickstart:graphql-spring-boot-starter:7.2.0'
  implementation 'com.expediagroup:graphql-kotlin-spring-server:2.1.2'
  implementation 'com.vladmihalcea:hibernate-types-52:2.12.1'
  implementation 'com.github.codefreak:templates:2.0.0'
  implementation 'com.networknt:json-schema-validator:1.0.60'
  implementation 'javax.validation:validation-api:2.0.1.Final'
  implementation 'org.hibernate:hibernate-validator:7.0.1.Final'
  implementation 'com.google.guava:guava:30.1.1-jre'
  implementation 'org.tukaani:xz:1.9'
  kapt 'org.springframework.boot:spring-boot-configuration-processor'
  runtimeOnly 'org.springframework.boot:spring-boot-devtools'
  runtimeOnly 'org.postgresql:postgresql'
  runtimeOnly 'org.hsqldb:hsqldb'
  testImplementation 'org.springframework.boot:spring-boot-starter-test'
  testImplementation 'org.springframework.security:spring-security-test'
  testImplementation 'org.hamcrest:hamcrest:2.2'
  testImplementation 'com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0'
  testImplementation 'com.tngtech.archunit:archunit-junit4:0.21.0'

  // workaround for https://github.com/spotify/docker-client/issues/1030
  implementation 'org.glassfish.jersey.inject:jersey-hk2:2.35'

  // Docker Client doesn't work with jersey-common >=2.29 because of missing Base64 class
  implementation('org.glassfish.jersey.core:jersey-common') {
    version {
      strictly '2.28'
    }
  }
  implementation('org.glassfish.jersey.core:jersey-client') {
    version {
      strictly '2.28'
    }
  }
  implementation('org.glassfish.jersey.connectors:jersey-apache-connector') {
    version {
      strictly '2.28'
    }
  }

  // workaround for https://github.com/mitreid-connect/OpenID-Connect-Java-Spring-Server/issues/1468
  implementation 'org.springframework.security.oauth:spring-security-oauth2:2.5.1.RELEASE'

  liquibaseRuntime 'org.liquibase.ext:liquibase-hibernate5:4.4.3'
  liquibaseRuntime 'com.h2database:h2:1.4.200'
  liquibaseRuntime 'org.yaml:snakeyaml:1.29'
  liquibaseRuntime sourceSets.main.runtimeClasspath
  liquibaseRuntime sourceSets.main.output
}

compileKotlin {
  kotlinOptions {
    allWarningsAsErrors = true
    freeCompilerArgs = ['-Xjsr305=strict']
    jvmTarget = '1.8'
  }
}

compileTestKotlin {
  kotlinOptions {
    allWarningsAsErrors = true
    freeCompilerArgs = ['-Xjsr305=strict']
    jvmTarget = '1.8'
  }
}

bootJar {
  // you can still build a fat jar for testing but this is NOT used for building
  // the Docker production image with jib!
  dependsOn 'deployFrontend'
}

spotless {
  kotlin {
    // Spotless cannot differentiate between generated (by Apollo) and regular sources
    // so we have to specify the files to analyse explicitly.
    target "src/*/kotlin/**/*.kt"
    ktlint().userData(['indent_size': '2'])
  }
}

jacoco {
  toolVersion = '0.8.4'
}

jacocoTestReport {
  reports {
    xml.enabled = true
    html.enabled = true
  }
}

springBoot {
  mainClassName = 'org.codefreak.codefreak.CodeFreakApplicationKt'
}

test {
  useJUnitPlatform()
  testLogging {
    events "failed"
    exceptionFormat "full"
  }
}

task vagrantTest(type: Test) {
  systemProperty "codefreak.docker.host", "http://localhost:2375"
  testLogging {
    events "failed"
    exceptionFormat "full"
  }
}

task generateGraphqlSchema(type: JavaExec) {
  classpath = sourceSets.main.runtimeClasspath
  main = "org.codefreak.codefreak.graphql.SchemaPrinterKt"
  args 'build/generated/graphql/schema.graphqls'
}

task deployFrontend(type: Copy) {
  from 'client/build'
  into 'build/resources/main/static'
  dependsOn "client:bundle"
}

jib {
  to {
    image = "ghcr.io/codefreak/codefreak"
  }
  container {
    mainClass = springBoot.mainClassName
    ports = ["8080"]
    // set image build date to current timestamp
    creationTime = "USE_CURRENT_TIMESTAMP"
    environment = [
      // default active profile is "dev"
      SPRING_PROFILES_ACTIVE: "prod",
      SENTRY_ENVIRONMENT: "prod",
      SENTRY_RELEASE: project.version.toString()
    ]
    volumes = [
      "/var/lib/codefreak"
    ]
    labels = [
      "org.opencontainers.image.source": "https://github.com/codefreak/codefreak"
    ]
  }
}
tasks.jibDockerBuild.dependsOn deployFrontend

apollo {
  schemaFile.set(project(":cloud-companion").file("src/main/resources/graphql/schema.graphqls"))
  packageName.set("org.codefreak.codefreak.cloud.workspace")
}
